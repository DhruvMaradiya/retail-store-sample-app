# wait until the cluster is really ready
resource "time_sleep" "wait_for_cluster_endpoint" {
  create_duration = "30s"
  depends_on      = [module.gke_autopilot]
}

# obtain cluster data *after* the wait
data "google_container_cluster" "autopilot" {
  name     = module.gke_autopilot.cluster_name
  location = var.gcp_region
  depends_on = [time_sleep.wait_for_cluster_endpoint]
}

data "google_client_config" "default" {}

# kubernetes provider configured late
provider "kubernetes" {
  host                   = "https://${data.google_container_cluster.autopilot.endpoint}"
  token                  = data.google_client_config.default.access_token
  cluster_ca_certificate = base64decode(data.google_container_cluster.autopilot.master_auth[0].cluster_ca_certificate)
}

# helm provider reuses the same credentials
provider "helm" {
  kubernetes {
    host                   = "https://${data.google_container_cluster.autopilot.endpoint}"
    token                  = data.google_client_config.default.access_token
    cluster_ca_certificate = base64decode(data.google_container_cluster.autopilot.master_auth[0].cluster_ca_certificate)
  }
}